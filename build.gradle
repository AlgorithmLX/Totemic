plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    id 'com.modrinth.minotaur' version '2.+'
}

version = mod_version
group = 'pokefenn.totemic'
archivesBaseName = 'Totemic-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mappings_channel, version: mappings_version
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'totemic'
            
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                totemic { sources sourceSets.main, sourceSets.test }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'totemic'
            
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                totemic { sources sourceSets.main, sourceSets.test }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'totemic'
            
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                totemic { sources sourceSets.main, sourceSets.test }
            }
        }
        
        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', 'totemic', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                totemic { source sourceSets.main }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://maven.blamejared.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.19.2-77:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.19.2-77")
}

task apiSrcJar(type: Jar) {
    from(sourceSets.main.java) {
        include 'pokefenn/totemic/api/**'
        appendix = 'api'
    }
    classifier = 'sources'
}

artifacts {
    archives apiSrcJar
}

jar {
    manifest {
        attributes([
            "Specification-Title": "totemic",
            "Specification-Vendor": "ljfa",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor": "ljfa",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = findProperty('curseForgeApiKey')
    
    def mainFile = upload(237541, jar)
    mainFile.releaseType = mod_release_type
    mainFile.changelog = file('changelog.md')
    mainFile.changelogType = 'markdown'
    mainFile.addOptional('patchouli')
    
    def apiSrcFile = mainFile.withAdditionalFile(apiSrcJar)
}

modrinth {
    token = findProperty('modrinthApiToken')
    projectId = 'fenns_totemic'
    versionType = mod_release_type
    uploadFile = jar
    changelog = file('changelog.md').text
    dependencies {
        optional.project 'patchouli'
    }
    additionalFiles = [apiSrcJar]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
